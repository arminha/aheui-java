apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

sourceCompatibility = 1.6

ext {
    mainClass = 'com.aha.aheui.interpreter.Main'
}

///////////////////////////////////////////////////
// Dependencies

repositories {
    mavenCentral()
}

dependencies{
    testCompile 'junit:junit:4.10'
}

///////////////////////////////////////////////////
// Packaging

jar {
    manifest {
        attributes 'Main-Class': mainClass
        def buildTag = System.getenv()['BUILD_TAG']
        if (buildTag != null && !buildTag.isEmpty()) {
            attributes 'Build-Tag': buildTag
        }
    }
}

///////////////////////////////////////////////////////
// Code Quality

findbugs {
    ignoreFailures = true
    toolVersion = '2.0.3'
}

if (!rootProject.hasProperty("findbugsReportXml") || findbugsReportXml != 'true') {
    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}

pmd {
    ignoreFailures = true
    toolVersion = '5.0.5'
}

checkstyle {
    ignoreFailures = true
    toolVersion = '5.7'
}

///////////////////////////////////////////////////////
// Wrapper

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
